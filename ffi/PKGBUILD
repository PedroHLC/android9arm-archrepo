# Created by: PedroHLC

_target=arm-linux-androideabi
_title=android-9-arm
_sysroot=/opt/android-ndk/platforms/android-9/arch-arm/usr/
_pkgname=libffi

_hostarch="$CARCH"
[ $CARCH == i686 ] && _hostarch="x86"

pkgname="${_title}-${_pkgname}"
pkgver=3.1
pkgrel=2
pkgdesc="Portable foreign function interface library (${_target})"
arch=('any')
makedepends=('android-ndk' make)
options=('!strip' '!buildflags' '!makeflags' 'staticlibs')
license=('MIT')
url='http://sourceware.org/libffi'
source=("ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz")
md5sums=('f5898b29bbfd70502831a212d9249d10')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  export NDK=/opt/android-ndk
  export SYSROOT=$NDK/platforms/android-9/arch-arm
  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig"
  export CFLAGS='-march=armv5' 
  export LDFLAGS='-Wl,--fix-cortex-a8'
  
  export _toolchainroot="$NDK/toolchains/${_target}-4.8/prebuilt/linux-${_hostarch}"
  export CC="${_toolchainroot}/bin/${_target}-gcc --sysroot=$SYSROOT"
  export STRIP="${_toolchainroot}/bin/${_target}-strip"
  export AR="${_toolchainroot}/bin/${_target}-ar"
  export LD="${_toolchainroot}/bin/${_target}-ld"
  export RANLIB="${_toolchainroot}/bin/${_target}-ranlib" 
  
  mkdir -p build_static && cd build_static
  ../configure \
      --prefix=${_sysroot} \
      --host=${_target} \
      --enable-static=yes --enable-shared=no
  make
  
  mkdir -p ../build && cd ../build
  ../configure \
      --prefix=${_sysroot} \
      --host=${_target} \
      --enable-static=no --enable-shared=yes
  make DESTDIR="${srcdir}/${_pkgname}-${pkgver}/build/output" install
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/${_pkgname}-${pkgver}/build_static"
  make DESTDIR="${pkgdir}" install
  
  rm -r "${pkgdir}/${_sysroot}/share/man"
  mv "${pkgdir}/${_sysroot}/lib/${_pkgname}-${pkgver}/include" "${pkgdir}/${_sysroot}/"
  rm -rf "${pkgdir}/${_sysroot}/lib/${_pkgname}-${pkgver}"
  rm "${pkgdir}/${_sysroot}/share/info/dir"
  "$STRIP" "${pkgdir}/${_sysroot}/lib/"*.so*
}

# vim:set ts=2 sw=2 et:
