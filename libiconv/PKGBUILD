# Created by: PedroHLC

_target=arm-linux-androideabi
_title=android-9-arm
_sysroot=/opt/android-ndk/platforms/android-9/arch-arm/usr/
_pkgname=libiconv

pkgname="${_title}-${_pkgname}"
pkgver=1.14
pkgrel=1
pkgdesc="Provides libiconv.so and libcharset.so (${_target})"
arch=('any')
url='http://www.gnu.org/software/libiconv/'
makedepends=('android-ndk' make)
license=('LGPL')
source=("http://ftp.gnu.org/pub/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('e34509b1623cec449dfeb73d7ce9c6c6')
options=(!strip !buildflags !libtool staticlibs)

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed '/LD_RUN_PATH/d' -i Makefile.in
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  export NDK=/opt/android-ndk
  export SYSROOT=$NDK/platforms/android-9/arch-arm
  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig"
  export CFLAGS='-march=armv5' 
  export LDFLAGS='-Wl,--fix-cortex-a8'
  
  export _toolchainroot="$NDK/toolchains/${_target}-4.8/prebuilt/linux-x86"
  export CC="${_toolchainroot}/bin/${_target}-gcc --sysroot=$SYSROOT"
  export STRIP="${_toolchainroot}/bin/${_target}-strip"
  export AR="${_toolchainroot}/bin/${_target}-ar"
  export LD="${_toolchainroot}/bin/${_target}-ld"
  export RANLIB="${_toolchainroot}/bin/${_target}-ranlib" 
  
  cp -f "$SYSROOT/usr/include/stdio.h" srclib/stdio.in.h
  ./configure \
	--prefix="${_sysroot}" --host=arm-linux-gnueabi \
	--enable-shared --enable-static
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/${_sysroot}/share/man"
  rm -r "$pkgdir/${_sysroot}/bin"
  rm "$pkgdir/${_sysroot}/lib/charset.alias"
  "$STRIP" "$pkgdir/${_sysroot}/lib/"*.so*
}
