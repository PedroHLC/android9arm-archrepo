# Created by: PedroHLC

_target=arm-linux-androideabi
_title=android-9-arm
_sysroot=/opt/android-ndk/platforms/android-9/arch-arm/usr/
_pkgname=libvorbis

pkgname="${_title}-${_pkgname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="Vorbis codec library (${_target})"
arch=('i686' 'x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=("${_title}-libogg")
makedepends=('android-ndk' make)
options=('!libtool' '!strip' '!buildflags')
source=(http://downloads.xiph.org/releases/vorbis/${_pkgname}-${pkgver}.tar.gz)
md5sums=('6b1a36f0d72332fae5130688e65efe1f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  export NDK=/opt/android-ndk
  export SYSROOT=$NDK/platforms/android-9/arch-arm
  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig"
  export CFLAGS='-march=armv5'
  export LDFLAGS='-Wl,--fix-cortex-a8'
  export CXXFLAGS="$CFLAGS -I\"$NDK/sources/cxx-stl/gnu-libstdc++/4.8/include\" \
  -I\"$NDK/sources/cxx-stl/gnu-libstdc++/4.8/libs/armeabi/include\" \
  -L\"$NDK/sources/cxx-stl/gnu-libstdc++/4.8/libs/armeabi\""
  
  export _toolchainroot="$NDK/toolchains/${_target}-4.8/prebuilt/linux-x86"
  export CC="${_toolchainroot}/bin/${_target}-gcc --sysroot=$SYSROOT"
  export CXX="${_toolchainroot}/bin/${_target}-g++ --sysroot=$SYSROOT"
  export CPP="${_toolchainroot}/bin/${_target}-cpp --sysroot=$SYSROOT"
  export STRIP="${_toolchainroot}/bin/${_target}-strip"
  export AR="${_toolchainroot}/bin/${_target}-ar"
  export LD="${_toolchainroot}/bin/${_target}-ld"
  export RANLIB="${_toolchainroot}/bin/${_target}-ranlib"
  
  LIBS+="-lm"  ./configure \
	--prefix="${_sysroot}" --host=arm-linux-gnueabi \
	--enable-shared --enable-static \
	--disable-oggtest
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  "$STRIP" "$pkgdir/${_sysroot}/lib/"*.so*
  #install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$_pkgname/LICENSE"
}
