_target=android-9-arm
_sysroot=/opt/android-ndk/platforms/android-9/arch-arm/usr/

_pkgname=sdl2
pkgname=${_target}-${_pkgname}
pkgver=2.0.0
pkgrel=2
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard - Version 2 (${_target})"
arch=('any')
url="http://www.libsdl.org"
license=('MIT')
makedepends=('android-ndk' cmake)
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")
options=(!buildflags !strip !libtool)
md5sums=('beec89afb6edcc6f0abc4114f2e6bcf7')

prepare() {
  cd "${srcdir}/SDL2-${pkgver}"
  
  sed 's/include\ $(BUILD_SHARED_LIBRARY)/include\ $(BUILD_STATIC_LIBRARY)/' < Android.mk > Android_static.mk
}

build() {
  cd "${srcdir}/SDL2-${pkgver}"
  
  ndk-build NDK_PROJECT_PATH=. APP_BUILD_SCRIPT=./Android_static.mk APP_PLATFORM=android-9
  ndk-build NDK_PROJECT_PATH=. APP_BUILD_SCRIPT=./Android.mk APP_PLATFORM=android-9
}

package() {
  srcdirfull="${srcdir}/SDL2-${pkgver}"
  pkgdirfull="$pkgdir/${_sysroot}"
  
  mkdir -p "${pkgdirfull}"
  cd "${pkgdirfull}"
  mkdir -p "./lib" "./include/SDL2"
  install -m644 "${srcdirfull}/include/"*.h ./include/SDL2
  rm ./include/SDL2/SDL_test*
  install -m644 "${srcdirfull}/libs/armeabi/"*.so ./lib/
  install -m644 "${srcdirfull}/obj/local/armeabi/"*.a ./lib/
}
